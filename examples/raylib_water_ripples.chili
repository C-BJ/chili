use raylib.*;
use c.*;

let width uint = 500;
let height uint = 350;
let max_droplet_interval i32 = 1;

let main = fn() {
	let mut prev [*mut]f64 = calloc(width * height, @size_of(f64)) as *mut f64 as _;
  	defer free(prev as *f64 as _);
	let mut curr [*mut]f64 = calloc(width * height, @size_of(f64)) as *mut f64 as _;
    defer free(curr as *f64 as _);

	InitWindow(width, height, "chili language - test - water ripples\0".data);
	SetTargetFPS(120);

	srand(time(nil));
	let mut frames = rand() % max_droplet_interval;

	while !WindowShouldClose() {
		BeginDrawing();

		ClearBackground(blue);

		if frames == 0 {
			droplet(prev, rand() % width, rand() % height);
			frames = rand() % max_droplet_interval;
		} else {
			frames -= 1;
		}

		if IsMouseButtonDown(0) {
            droplet(prev, GetMouseX(), GetMouseY());
		}

		for x in 1..width-2 {
			for y in 1..height-2 {
				let mut value = (
					(prev[(x - 1) + y * width] +
					prev[(x + 1) + y * width] +
					prev[x + (y - 1) * width] +
					prev[x + (y + 1) * width]) / 2 - 
					curr[x + y * width]
				);
				
				value *= frand(0.75, 0.99);

                let value_u8 = value as u8;
				let color = Color {
					r = value_u8,
					g = value_u8,
					b = value_u8,
					a = (value_u8 as f64 * 0.75) as _,
				};

				DrawPixel(x, y, color);

                curr[x + y * width] = value;
			}
		}

		let temp = prev;
		prev = curr;
		curr = temp;

        DrawFPS(10, 10);

		EndDrawing();
	}

	CloseWindow();
}

let droplet = fn(buf [*mut]f64, x i32, y i32) {
	if  x >= 1 &&
		x <= width - 1 &&
		y >= 1 &&
		y <= height - 1 {
		buf[x + y * width] = 255;
	}
}

let frand = fn(min f64, max f64) -> f64 {
    let scale = (rand() as f64) / (RAND_MAX as f64);
    min + scale * (max - min)
}

let RAND_MAX uint = 32767;