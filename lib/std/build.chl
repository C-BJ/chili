let { panic } = import lib
pub let { os, Os_linux, Os_windows } = import intrinsics
let { printf, waitpid, fork, execl } = import c

@intrinsic
pub extern fn start_workspace(workspace: Workspace) -> (*str, bool)

pub type Workspace = struct {
    name: *str
    build_options: BuildOptions
}

pub type BuildOptions = struct {
    input_file: *str
    output_file: *str
    target: BuildTarget
    optimization_level: OptimizationLevel
}

# TODO: turn into enum
pub type BuildTarget = uint
pub let BuildTarget_auto: BuildTarget = 0
pub let BuildTarget_linux: BuildTarget = 1
pub let BuildTarget_windows: BuildTarget = 2

# TODO: turn into enum
pub type OptimizationLevel = uint
pub let OptimizationLevel_debug: OptimizationLevel = 0
pub let OptimizationLevel_release: OptimizationLevel = 1

pub fn run_output_file(output_file: *str) = {
    if os == Os_linux {
        let pid = fork()

        if pid == 0 {
            # child process
            execl(output_file.data, 0)
        } else if pid > 0 {
            # parent process
            waitpid(pid, (0 as int) as _, 0)
        } else {
            # error
            panic("fork failed")
        }
    } else if os == Os_windows {
        	let w = std.sys.windows

		let si = w.types.STARTUPINFO {
			cb: size_of!(w.types.STARTUPINFO) as _,
			lpReserved: 0 as uint as _,
			lpDesktop: 0 as uint as _,
			lpTitle: 0 as uint as _,
			dwX: 0,
			dwY: 0,
			dwXSize: 0,
			dwYSize: 0,
			dwXCountChars: 0,
			dwYCountChars: 0,
			dwFillAttribute: 0,
			dwFlags: 0,
			wShowWindow: 0,
			cbReserved2: 0,
			lpReserved2: 0 as uint as _,
			hStdInput: 0,
			hStdOutput: 0,
			hStdError: 0,
		}

		let mut pi = w.types.PROCESS_INFORMATION {
			hProcess: 0,
			hThread: 0,
			dwProcessId: 0,
			dwThreadId: 0,
		}

		if w.kernel32.CreateProcessA(
			output_file.data,
			0 as uint as _, # null
			0 as uint as _, # null
			0 as uint as _, # null
			w.types.FALSE,
			0,
			0 as uint as _, # null
			0 as uint as _, # null
			&si,
			&mut pi,
		) != 0 {
			w.kernel32.WaitForSingleObject(pi.hProcess, w.types.INFINITE)
			w.kernel32.CloseHandle(pi.hProcess)
			()
		} else {
			std.c.printf("error = %d\n".data, w.kernel32.GetLastError())
			panic("CreateProcessA failed")
		}
    } else {
        # TODO: when panic formatting is implemented, use it here
        printf("os = %d\n".data, os)
        panic("unsupported OS")
    }

    ()
}